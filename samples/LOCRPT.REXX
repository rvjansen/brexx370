/* REXX */
 
LOC_DATA="LOCATE.DATA"
 
OFFS=DCL('$DEFINE','LOC')
OFFS=DCL('HLQ',1,20,'CHAR')
OFFS=DCL('VOL',,6,'CHAR')
OFFS=DCL('DSET',,44,'CHAR')
OFFS=DCL('DSO',,2,'CHAR')
OFFS=DCL('LRECL',,5,'CHAR')
OFFS=DCL('MEMBER',,8,'CHAR')
 
OFFS=DCL('$DEFINE','MEMB')
OFFS=DCL('LMARGIN',1,14,'CHAR')
OFFS=DCL('MEMBER1',,15,'CHAR')
OFFS=DCL('MEMBER2',,15,'CHAR')
OFFS=DCL('MEMBER3',,15,'CHAR')
OFFS=DCL('MEMBER4',,15,'CHAR')
OFFS=DCL('MEMBER5',,15,'CHAR')
OFFS=DCL('MEMBER6',,15,'CHAR')
OFFS=DCL('MEMBER7',,15,'CHAR')
 
LAST_DSET=''
CURRENT_LINE=1
MAX_LINES=60
PAGE_COUNT=0
 
PAGE_HEADER.=''
PAGE_HEADER.0=0
 
MEMBER_LINES.=''
MEMBER_LINES.0=0
 
MEMBER_SECTION=0    /* FLAG WHILE PRINTING THE MEMBER LISTING */
 
/* OPEN THE DATA FILE */
DS=OPEN(LOC_DATA,"R")
IF DS<0 THEN DO
   SAY 'COULD NOT OPEN THE LOCATE DATASET ('DS')'
   EXIT
   END
SAY 'START TIME: 'TIME()
SAY 'GENERATING YOUR REPORT ON THE CLASS A PRINTER'
ADDRESS TSO
"ALLOCATE F(PRINTER),SYSOUT(A)"
SAY 'PRINTER ALLOCATED'
CALL FORMAT_TOP
DO FOREVER
    LINEIN=READ(DS)
    IF STRIP(LINEIN)='' THEN LEAVE
    CALL SPLITRECORD 'LOC',LINEIN
 
    IF LAST_DSET='' THEN
        DO
            /* THIS IS THE FIRST DATASET */
            LAST_DSET=DSET
            CURR_HLQ=HLQ
            CURR_DSET=DSET
            CURR_VOL=VOL
            CURR_DSO=DSO
            CURR_LRECL=LRECL
            DSET_MEMBERS=0
            DSMEMBERS.=''
            DSMEMBERS.0=0
        END
 
    IF DSET=LAST_DSET THEN
        DO
 
            /* IS THERE A MEMBER SPECIFIED? */
            IF STRIP(MEMBER)<>'' THEN /* A MEMBER IS PRESENT */
                DO
                    DSET_MEMBERS=DSET_MEMBERS+1
                    DSMEMBERS.DSET_MEMBERS=MEMBER
                    DSMEMBERS.0=DSET_MEMBERS
                END
        END
    ELSE
        DO
            /* GENERATE THE PAGE(S) AND SEND IT TO OUTPUT */
            CALL FORMAT_DATASET
            CALL FORMAT_MEMBERS
 
            LAST_DSET=DSET
            DROP DSMEMBERS
            DSET_MEMBERS=0
            /* THIS IS THE FIRST TIME FOR THIS DATASET */
            /* PUT THE CURRENT DATASET INFORMATION INTO MEMORY */
            CURR_HLQ=HLQ
            CURR_DSET=DSET
            CURR_VOL=VOL
            CURR_DSO=DSO
            CURR_LRECL=LRECL
            /* IS THERE A MEMBER SPECIFIED? */
            IF STRIP(MEMBER)<>'' THEN /* A MEMBER IS PRESENT */
                DO
                    DSET_MEMBERS=DSET_MEMBERS+1
                    DSMEMBERS.DSET_MEMBERS=MEMBER
                    DSMEMBERS.0=DSET_MEMBERS
                END
        END
 
 
END
CALL CLOSE('PRINTER')
ADDRESS TSO
"FREE F(PRINTER)"
SAY 'PRINTER RELEASED.'
SAY 'END TIME IS: 'TIME()
SAY 'ELAPSED: 'TIME('ELAPSED')' SECONDS.'
CALL CLOSE(DS)
EXIT
 
 
 
 
 
 
 
/* ROUTINES AND FUNCTIONS */
 
PRINTCR:
ARG OUTLINE
   CALL WRITE('PRINTER',OUTLINE,NL)
   CURRENT_LINE=CURRENT_LINE+1
   IF CURRENT_LINE>(MAX_LINES-3) THEN CALL END_OF_PAGE
RETURN CURRENT_LINE
 
END_OF_PAGE:
/* IF WE HAVE LESS THAN 5 LINES TO THE END OF PAGE, SKIP THEM */
    IF CURRENT_LINE<(MAX_LINES-5) THEN RETURN
    DO FF=CURRENT_LINE TO (MAX_LINES+1)
        CALL WRITE('PRINTER',' ',NL)
    END
    CURRENT_LINE=1
    CALL FORMAT_TOP
RETURN
 
FORMAT_TOP:
    DROP HEADER.
    PAGE_COUNT=PAGE_COUNT+1
    HEADER.=''
    HEADER.1=COPIES('*',132)
    HEADER_LINE=CENTER('DATASET AND MEMBER LISTING',132)
    HEADER_LINE=OVERLAY('PAGE: '||PAGE_COUNT,HEADER_LINE,120)
    HEADER.2=HEADER_LINE
    HEADER.3=COPIES('*',132)
    HEADER.4=' '
    HEADER.0=4
    DO PH=1 TO HEADER.0
        CALL PRINTCR(HEADER.PH)
    END
    IF MEMBER_SECTION=1 THEN
        DO
            CALL PRINTCR(' ')
            CALL PRINTCR(CURR_DSET||' (CONTINUED)')
            CALL PRINTCR(' ')
        END
RETURN
 
FORMAT_DATASET:
   OUTLINE=COPIES(' ',132)  /* 132 CHAR LINE */
   OUTLINE=OVERLAY('HLQ: '||CURR_HLQ,OUTLINE,1)
   OUTLINE=OVERLAY('DATASET: '||CURR_DSET,OUTLINE,30)
   OUTLINE=OVERLAY('VOLUME: '||CURR_VOL,OUTLINE,90)
   OUTLINE=OVERLAY('ORG/LRECL: '||CURR_DSO||'/'||CURR_LRECL,OUTLINE,110)
   CALL PRINTCR(OUTLINE)
 
RETURN
 
FORMAT_MEMBERS:
    MEMBER_SECTION=1
    DROP MBR_REPORT.
    MBR_REPORT.=''
    MBR_REPORT.0=0
    MEMBER_LINES=0
    MY_MEMBER=0
    THISLINE=COPIES(' ',100)
    CALL PRINTCR('')
    CALL PRINTCR(COPIES('-',61)||' MEMBERS '||COPIES('-',62))
    CALL PRINTCR('')
    DO I=1 TO DSMEMBERS.0
        MY_MEMBER=MY_MEMBER+1
        IF MY_MEMBER>7 THEN
            DO
                /* START A NEW LINE */
                MEMBER_LINES=MEMBER_LINES+1
                MBR_REPORT.MEMBER_LINES=THISLINE
                MBR_REPORT.0=MEMBER_LINES
                THISLINE=COPIES(' ',100)
                MY_MEMBER=1
            END
        OFFSET=MY_MEMBER*15
        THISLINE=OVERLAY(DSMEMBERS.I,THISLINE,OFFSET)
    END
    IF STRIP(THISLINE)<>'' THEN
        DO
            /* THERE'S LEFTOVER DATA */
            MEMBER_LINES=MEMBER_LINES+1
            MBR_REPORT.MEMBER_LINES=THISLINE
            MBR_REPORT.0=MEMBER_LINES
        END
    DO ML=1 TO MBR_REPORT.0
        CALL PRINTCR(MBR_REPORT.ML)
    END
    CALL PRINTCR(' ')
    MEMBER_SECTION=0
    CALL END_OF_PAGE
RETURN
 
 