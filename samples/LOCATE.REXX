/* REXX                                        */
/* SCRIPT TO GENERATE THE DATA FILE OF ALL     */
/* DATASETS, AND ANY MEMBERS CONTAINED IN THEM */
/*---------------------------------------------*/
/* SCOTT JOHNSON - WESTDALEFARMER@GMAIL.COM    */
/*---------------------------------------------*/
 
SAY 'START TIME IS: 'TIME()
OFFS=DCL('$DEFINE','LOC')
OFFS=DCL('HLQ',1,20,'CHAR')
OFFS=DCL('VOL',,6,'CHAR')
OFFS=DCL('DSET',,44,'CHAR')
OFFS=DCL('DSO',,2,'CHAR')
MEMBER_OFFSET=DCL('LRECL',,5,'CHAR')
OFFS=DCL('MEMBER',,8,'CHAR')
 
OFFS=DCL('$DEFINE','VTOCOUT')
OFFS=DCL('VDSNAME',1,44,'CHAR')
OFFS=DCL('VDSO',46,2,'CHAR')
OFFS=DCL('VLRECL',50,5,'CHAR')
OFFS=DCL('VVOL',56,6,'CHAR')
 
DROP VTOC.
VTOC.=''
VTOC.0=0
CALL OUTTRAP('VTOC.')
ADDRESS TSO 'VTOC ALL SORT PRINT(NEW (DSNAME DSO LRECL VOLUME))'
CALL OUTTRAP('OFF')
 
/* VTOC. NOW HAS ALL DSNAMES AND A FEW TITLE/TOTAL LINES */
/* THE FIRST THREE LINES ARE HEADERS */
/* THE LAST LINE IS TITLES */
/* INTERMIXED ARE HEADERS THAT BEGIN 'DSNAME' */
SAY 'FOUND 'VTOC.0-5' DATASETS ON ALL VOLUMES.'
SAY 'PROCESSING...  (THIS MAY TAKE A WHILE)'
/* CREATE THE INITIAL WORK FILE */
FH=OPENDSN("LOCATE.WORK")
DO I=4 TO (VTOC.0)-1
   /* FIND OUT IF THIS IS A HEADER LINE */
   CHECK=STRIP(SUBSTR(VTOC.I,1,8))
   IF CHECK='DSNAME' THEN ITERATE  /* ITS A HEADER */
   /* IF NOT, IT'S A DATA LINE */
   IF POS('SYS1.SMP',VTOC.I)>0 THEN DO
      SAY 'SKIPPING 'VTOC.I' DATASET.'
      ITERATE
      END
   IF POS('SYS1.LOGREC',VTOC.I)>0 THEN DO
      SAY 'SKIPPING 'VTOC.I' DATASET.'
      ITERATE
      END
   CALL SPLITRECORD 'VTOCOUT',VTOC.I
   IF VLRECL=0 THEN VDSO='U '
   IF VDSO='PS' THEN ITERATE
   CALL WRITE(FH,SETRECORD('VTOCOUT'),NL)
END
/* NOW DROP THE VTOC. STEM TO FREE UP MEMORY */
DROP VTOC.
CALL CLOSE(FH)
/* OPEN THE WORK DATASET */
WD=OPEN("LOCATE.WORK","R")
/* OPEN THE OUTPUT DATASET */
OD=OPENDSN("LOCATE.DATA")
IF WD<0 THEN SAY "COULD NOT OPEN WORK DATASET."
IF OD<0 THEN SAY "COULD NOT OPEN OUTPUT DATASET."
IF (WD<1)|(OD<1) THEN ABEND(12)
 
DO FOREVER
  LINEIN=READ(WD)
  IF STRIP(LINEIN)='' THEN LEAVE
  CALL SPLITRECORD 'VTOCOUT',LINEIN
  IF STRIP(VDSO)="PS" THEN ITERATE
  CALL DIR("'"STRIP(VDSNAME)"'",'MEMBERS')
  IF DIRENTRY.0 < 1 THEN DO
     DROP DIRENTRY.
     ITERATE
  END
  HLQ=SUBSTR(VDSNAME,1,POS('.',VDSNAME)-1)
  VOL=VVOL
  DSET=STRIP(VDSNAME)
  DSO=VDSO
  MEMBER=''
  LRECL=RIGHTADJ(STRIP(VLRECL),5)
  OUTREC=SETRECORD('LOC')
  DO I=1 TO DIRENTRY.0
      MEMBER=STRIP(DIRENTRY.I.NAME)
      OUTREC=OVERLAY(MEMBER,OUTREC,MEMBER_OFFSET)
      CALL WRITE(OD,OUTREC,NL)
  END
  DROP DIRENTRY.
END
CALL CLOSE(WD)
CALL CLOSE(OD)
RC=REMOVE("LOCATE.WORK")
SAY 'END TIME IS 'TIME()
SAY 'ELAPSED: 'TIME('ELAPSED')' SECONDS.'
RETURN 0
 
RIGHTADJ:
   ARG INSTR,LEN
   MYLEN=LENGTH(INSTR)
   IF MYLEN>LEN THEN DO
     SAY INSTR' IS TOO LONG TO RIGHT ADJUST.'
     RETURN INSTR
     END
   PADLEN=LEN-MYLEN
   IF PADLEN <0 THEN SAY PADLEN
   IF PADLEN >LEN THEN SAY PADLEN
   NEWSTR=COPIES(' ',PADLEN)
   NEWSTR=NEWSTR||INSTR
   IF LENGTH(NEWSTR)<>LEN THEN
     DO
       SAY 'INVALID RETURN.'
     END
RETURN NEWSTR
OPENDSN:
  PARSE ARG CDSN
  IF EXISTS(CDSN)=0 THEN DO
     RC=CREATE(CDSN,'RECFM=FB,LRECL=128,BLKSIZE=512,
        UNIT=SYSDA,PRI=500,DIRBLKS=0')
     IF RC<>0 THEN DO
        SAY "CANNOT CREATE "LOC_DATA
        EXIT
     END
  END
  FO=OPEN(CDSN,"W")
  IF FO<0 THEN DO
     SAY "UNABLE TO OPEN DATASET ("CDSN")"
     ABEND(12)
  END
RETURN FO